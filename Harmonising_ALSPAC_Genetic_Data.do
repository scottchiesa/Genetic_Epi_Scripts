*************************************************************************************************
*************Script for Harmonising Genetic Data Before Creating PRS in ALSPAC*******************
*************************************************************************************************

**Prepare GWAS File**

**The following works on a .csv file that starts with the following columns:
	** rsid, chrpos, effect_alleleea, non_effect_allele, ea_freq, beta, se, pvalue

import delimited "/home/rmgpstc/Scratch/ALSPAC/Genotyping_data/Converted_data/WMH_GWAS_SNPS.csv", clear

rename effect_alleleea effect_allele

foreach var of varlist * {
	if "`var'" != "chrpos" {
		rename `var' GWAS_`var'
	}
}

save "GWAS_SNPS.dta", replace

**Prepare ALSPAC File**

** The file used here comes from a file previously generated by PLINK at an earlier stage of the Creating_WMH_PRS.sh file in which this code will run

import delimited "/home/rmgpstc/Scratch/ALSPAC/Genotyping_data/Converted_data/PRS_SNPS_maf.afreq", clear

drop chrom
rename id chrpos
rename alt effect_allele
rename ref non_effect_allele 
rename alt_freqs ea_freq
drop obs_ct

foreach var of varlist * {
	if "`var'" != "chrpos" {
		rename `var' ALSPAC_`var'
	}
}

save "ALSPAC_SNPS.dta", replace

**Merge Files

merge m:1 chrpos using "GWAS_SNPS.dta"
keep if _merge ==3
drop _merge

order chrpos ALSPAC_effect_allele ALSPAC_non_effect_allele ALSPAC_ea_freq GWAS_effect_allele GWAS_non_effect_allele GWAS_ea_freq GWAS_beta GWAS_se GWAS_pvalue

**Browse data

list chrpos ALSPAC_effect_allele ALSPAC_non_effect_allele ALSPAC_ea_freq GWAS_effect_allele GWAS_non_effect_allele GWAS_ea_freq in 1/10

**Look for SNPs with different positions**

gen diff_pos = (GWAS_effect_allele != ALSPAC_effect_allele)
tab diff_pos
list chrpos GWAS_effect_allele ALSPAC_effect_allele if diff_pos == 1

**Reverse betas if SNPs in different positions**

gen GWAS_beta_rev = GWAS_beta if diff_pos == 0
replace GWAS_beta_rev = GWAS_beta*-1 if diff_pos == 1

**Switch alleles for any SNPs that were in wrong direction and now flipped**
gen temp = GWAS_effect_allele 
replace GWAS_effect_allele = GWAS_non_effect_allele if diff_pos == 1
replace GWAS_non_effect_allele = temp if diff_pos == 1 
drop temp 

*Then switch EAF for these too as effect allele is changing**
replace GWAS_ea_freq = 1-GWAS_ea_freq if diff_pos == 1

** Check for palindromic SNPs to see if any need to be removed**
list chrpos GWAS_ea_freq ALSPAC_ea_freq if GWAS_effect_allele == "A" & GWAS_non_effect_allele == "T"
list chrpos GWAS_ea_freq ALSPAC_ea_freq if GWAS_effect_allele == "T" & GWAS_non_effect_allele == "A"
list chrpos GWAS_ea_freq ALSPAC_ea_freq if GWAS_effect_allele == "C" & GWAS_non_effect_allele == "G"
list chrpos GWAS_ea_freq ALSPAC_ea_freq if GWAS_effect_allele == "G" & GWAS_non_effect_allele == "C"

**Drop any palindromic SNPs that have EAFs of ~0.5**
//drop if chrpos == "X:XXXXXXXX"
//drop if chrpos == "X:XXXXXXXXX"

count if !missing(chrpos)

**Check that all seems to line up**
list chrpos ALSPAC_effect_allele GWAS_effect_allele GWAS_beta GWAS_beta_rev if diff_pos == 0
list chrpos ALSPAC_effect_allele GWAS_effect_allele GWAS_beta GWAS_beta_rev if diff_pos == 1

corr GWAS_ea_freq ALSPAC_ea_freq
gen ALSPAC_ea_freq_2 = ALSPAC_ea_freq if diff_pos == 0
replace ALSPAC_ea_freq = 1 - ALSPAC_ea_freq if diff_pos == 1
corr GWAS_ea_freq ALSPAC_ea_freq_2

**Get rid of any unnecessary variables**
drop ALSPAC_ea_freq_2 diff_pos GWAS_beta
rename GWAS_beta_rev GWAS_beta

save "GWAS_ALSPAC_SNPS.dta", replace

**Export a file for use in Plink**

preserve
drop ALSPAC_ea_freq - GWAS_rsid
export delimited using "/home/rmgpstc/Scratch/ALSPAC/Genotyping_data/Converted_data/Cleaned_GWAS_SNPs.txt", delimiter(" ") replace
restore